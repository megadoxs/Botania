plugins {
    id 'com.diffplug.spotless' version '5.12.5' apply false
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'pmd'
    apply plugin: "com.diffplug.spotless"
    apply plugin: 'maven-publish'
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    archivesBaseName = "${mod_name}"

    repositories {
        mavenCentral()

        maven {
            name = 'Mixin (Fabric fork)'
            url = 'https://maven.fabricmc.net/'
        }

        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org/'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        it.enabled = false
    }

    pmd {
        toolVersion '6.35.0'
        incrementalAnalysis.set(true)
        ruleSetFiles = rootProject.files("spotless/pmd-ruleset.xml")
    }

    jar {
        from "LICENSE"

        exclude "**/*.bat"
        exclude "**/*.psd"
        exclude "**/*.ase"
        exclude "**/*.exe"
        exclude "**/unused"

        exclude ".cache"
    }

    spotless {
        enforceCheck = false // workaround around jenkins having wrong line endings in some files
        // we explicitly check in github actions builds
        java {
            indentWithTabs()
            eclipse().configFile rootProject.file('spotless/eclipseformat.xml')
            removeUnusedImports()
            importOrderFile rootProject.file('spotless/eclipse.importorder')
        }
    }
}
