plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}

loom {
    accessWidenerPath = file("src/main/resources/botania.accesswidener")
    mixin.defaultRefmapName = "botania.refmap.json"

    runs {
        gameTest {
            server()
            vmArg "-Dfabric-api.gametest=1"
        }

        xplatDatagen {
            client()
            vmArg "-Dbotania.xplat_datagen=1"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=botania"
            vmArg "-Dfabric-api.datagen.output-dir=${file("../Xplat/src/generated/resources")}"
        }

        fabricDatagen {
            client()
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=botania"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
        }

        configureEach {
            runDir "../run"
            ideConfigGenerated(true)
        }
    }

}

repositories {
    maven {
        name = "Modmuss"
        url = "https://maven.modmuss50.me/"
    }
    maven {
          name = "Ladysnake Libs"
          url = 'https://maven.ladysnake.org/releases'
    }
    maven {
        name = "BuildCraft" // for LBA
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        name = "entity reach"
        url = "https://maven.jamieswhiteshirt.com/libs-release/"
    }
    maven {
        url = "https://jitpack.io" // for step height eam
    }
    maven {
        url = "https://maven.shedaniel.me/" // for rei
    }
    maven {
        name = "ParchmentMC"
        url = "https://maven.parchmentmc.net/"
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven" // for "here be no dragons"
        content {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modImplementation "vazkii.patchouli:Patchouli:${patchouli_version}-FABRIC"

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}"

    modCompileOnly "dev.emi:emi-fabric:${emi_version}:api"
    modLocalRuntime "dev.emi:emi-fabric:${emi_version}"

    modImplementation "me.zeroeightsix:fiber:0.23.0-2"
    include "me.zeroeightsix:fiber:0.23.0-2"

    // Don't include base and entity as Trinkets includes it already
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}"

    //compileOnly "com.demonwav.mcdev:annotations:1.0" // only to shut up compile warnings from CCA's usage of CheckEnv and Env

    modImplementation("dev.emi:trinkets:${trinkets_version}") { transitive = false }

    //modImplementation "com.jamieswhiteshirt:reach-entity-attributes:2.5.0"
    //include "com.jamieswhiteshirt:reach-entity-attributes:2.5.0"

    //modImplementation "com.github.emilyploszaj:step-height-entity-attribute:v1.2.0"
    //include "com.github.emilyploszaj:step-height-entity-attribute:v1.2.0"

    modCompileOnly('teamreborn:energy:4.1.0') { transitive = false }

    // modCompileOnly 'com.blamejared.crafttweaker:CraftTweaker-fabric-1.18.2:9.1.123'

    modCompileOnly "com.unascribed:ears-api:${ears_version}"

    compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"

    modRuntimeOnly "maven.modrinth:here-be-no-dragons:1.0.0"

    compileOnly group: 'org.jetbrains', name: 'annotations', version: '26.0.2'
}
