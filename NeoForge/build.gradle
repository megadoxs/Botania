plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

repositories {
    maven {
        name = "C4"
        url = "https://maven.theillusivec4.top/"
    }
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':Xplat').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }

    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }

    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }

        client {
            client()
        }
        data {
            data()
        }
        server {
            server()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    implementation "vazkii.patchouli:Patchouli:${patchouli_version}-NEOFORGE"
    compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    // runtimeOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"

    compileOnly "dev.emi:emi-neoforge:${emi_version}:api"
    runtimeOnly "dev.emi:emi-neoforge:${emi_version}"

    compileOnly "top.theillusivec4.curios:curios-neoforge:${curios_version}:api"
    runtimeOnly "top.theillusivec4.curios:curios-neoforge:${curios_version}"

    compileOnly "com.unascribed:ears-api:${ears_version}"

    compileOnly group: 'org.jetbrains', name: 'annotations', version: '26.0.2'
}
